///////
// Sets

%s PVar {x, r, pr, rn, pl, l} // Names of pointer variables.
%s HVar {x}  // Names of pointer variables used for taking heap snapshots.
%s HLoc {L0} // Names of program labels where snapshots of the heap are taken.

#include "predicates.tvp"

%%

#include "actions.tvp"

%%

////////////////////////////////////////////////////////////////////
// Transition system for an erroneous implementation of insert sort,
// which creates a memory leak.

                                 // L insertSort_bug1(L x) {
L0  Copy_Reach_L(x, L0)     L1   // [ Take a snapshot of the heap to later         ]
                                 // [ verify that the output list is a permutation ]
                                 // [ of the list pointed by x.                    ]
L1  Copy_Var_L(r, x)        L2   //   r = x;
L2  Set_Null_L(pr)          L3   //   pr = NULL;
L3  Is_Not_Null_Var(r)      L4   //   while (r != NULL) { 
L3  Is_Null_Var(r)          L23
L4  Copy_Var_L(l, x)        L6   //     l = x;
L6  Set_Null_L(pl)          L7   //     pl = NULL;
L7  Is_Not_Eq_Var(l, r)     L8   //     while (l != r) {
L7  Is_Eq_Var(l, r)         L21
L8  Greater_Data_L(l, r)    L9   //       if (l->d > r->d) {
L8  Less_Equal_Data_L(l, r) L19
L9  Get_Next_L(rn, r)       L10  //         rn = r->n;
L10 Set_Next_Null_L(pr)     L11  //         pr->n = NULL;
L11 Set_Next_L(pr, rn)      L12  //         pr->n = rn;
L12 Set_Next_Null_L(r)      L13  //         r->n = NULL;
L13 Set_Next_L(r, l)        L14  //         r->n = l;
L14 Is_Null_Var(pl)         L15  //         if (pl == NULL)
L14 Is_Not_Null_Var(pl)     L16
L15 Copy_Var_L(x, r)        L18  //           x = r;
                                 //         else {
L16 Set_Next_Null_L(pl)     L17  //           pl->n = NULL;
L17 Set_Next_L(pl, r)       L18  //           pl->n = r; }
L18 Copy_Var_L(r, pr)       L21  //         r = pr;
                                 //         break;
                                 //       }
L19 Copy_Var_L(pl, l)       L20  //       pl = l;
L20 Get_Next_L(l, l)        L7   //       l = l->n;
                                 //     }
L21 Copy_Var_L(pr, r)       L22  //     pr = r;
L22 Get_Next_L(r, r)        L3   //     r = r->n;
                                 //   }
L23 Set_Null_L(l)           L24  //   l = NULL;
L24 Set_Null_L(r)           L25  //   r = NULL;
L25 Set_Null_L(pr)          L26  //   pr = NULL;
L26 Set_Null_L(pl)          L27  //   pl = NULL;
L27 Set_Null_L(rn)          exit //   rn = NULL;
                                 //   return x;
                                 // }
exit Assert_Permutation_L(x, L0) exit2
exit Assert_Sorted_L(x)          exit2

%%

/////////////////////////////////////////////////////////////////////////
// Display structures that arise only at the following program locations.
L1, exit