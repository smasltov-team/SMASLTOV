#define CondAssign(cond,pred,result) pred = ((cond) ? (result) : (pred))
#define UPDATE_AT(from,to)\
    CondAssign(tr(t),at[from](t),0)\
    CondAssign(tr(t),at[to](t),1)\


//P(Semaphore s)
//{
//  wait until s > 0, then s := s-1;
//  /* must be atomic once s > 0 is detected */
//}
%action P_fail(from, to, semaphore) {
  %t "P(" + semaphore + ") fail"
  %p !semaphore()
  {
    UPDATE_AT(from,to)  
  }
}

%action P_success(from, to, semaphore) {
  %t "P(" + semaphore + ") success"
  %p semaphore()
  {
    semaphore() = !semaphore()
    UPDATE_AT(from,to)
  }
}

//V(Semaphore s)
//{
//  s := s+1;   /* must be atomic */
//}
%action V(from, to, semaphore) {
  %t "V(" + semaphore + ")"
  %message (semaphore()) -> "Attempt to increment binary semaphore " + semaphore + " beyond 1"
  {
    semaphore() = 1

    UPDATE_AT(from,to)
  }
}

%action increment(from, to, counter) {
  %t counter + "=" + counter + "+1"
  %f {counter(n), counter(n) & successor(n,n_1), counter(n) & successor(n_1,n)}
  {
    counter(n) = E(n_1) counter(n_1) & successor(n_1,n)

    UPDATE_AT(from,to)
  }
}

%action decrement(from, to, counter) {
  %t counter + "=" + counter + "-1"
  %f {counter(n), counter(n) & successor(n_1,n), counter(n) & successor(n,n_1)}
  %message (E(n) counter(n) & num0(n)) -> "Attempt to decrement a 0-valued counter: " + counter
  {
    counter(n) = E(n_1) counter(n_1) & successor(n,n_1)

    UPDATE_AT(from,to)
  }
}

%action if_equal(from, to, counter,value) {
  %t counter + "==" + value
  %f {counter(n), value(n),
      counter(n) & successor(n,n_1), counter(n) & successor(n_1,n),
      value(n) & successor(n,n_1), value(n) & successor(n_1,n)}
  %p E(n) counter(n) & value(n)
  {
    UPDATE_AT(from,to)
  }
}

%action if_not_equal(from, to, counter,value) {
  %t counter + "!=" + value
  %f {counter(n), value(n),
      counter(n) & successor(n,n_1), counter(n) & successor(n_1,n),
      value(n) & successor(n,n_1), value(n) & successor(n_1,n)}
  %p !(E(n) counter(n) & value(n))
  {
    UPDATE_AT(from,to)
  }
}

%action read(from, to) {
  %t "reading"
  {
    UPDATE_AT(from,to)
  }
}

%action write(from, to) {
  %t "writing"
  {
    UPDATE_AT(from,to)
  }
}
