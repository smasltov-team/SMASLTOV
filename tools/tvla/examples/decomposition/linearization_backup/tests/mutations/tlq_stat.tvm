/******************************************************************
* tlq_stat.tvm
*
* assumptions:
* 1. thread objects can not be referenced by any other object (they are like globals)
* 2. the list is acyclic
*
******************************************************************/

/*******************
* skip()
********************/
%action skip() {
    %t "skip"
    %f { runnable(tr) }
}



//NOTE: We assume that a node can exist only in the concurrent heap and not in
//the sequential heap (and is then marked by dnode) but not vice versa.
/*******************
* assignNew(ref)
* [ref = new T()]
* Allocation to local variable.
* note - the type is currently ignored.
********************/
//ref isn't assumed to be null before allocation.
%action assignNew(ref,trnm) {
    %t ref + "= new T()"
    %f { runnable(tr) } 
    %new 
    {

        r_by[ref,trnm](v) = isNew(v)

        dnode(v) = dnode(v) | isNew(v)
        
        //updates due to universe-changing action:

        foreach (fld in NextFields) {
              rv[fld](o_1,o_2) = rv[fld](o_1,o_2)
        }

        foreach (fld in QFields) {
              r_by[fld](o) = r_by[fld](o)
        }

        foreach (nm in StaticThreadNames-{trnm}) {
             foreach (fld in LocalFields) {
                r_by[fld,nm](o) = r_by[fld,nm](o)
             }
        }

        foreach (fld in LocalFields-{ref}) {
            r_by[fld,trnm](v) = r_by[fld,trnm](v)
        }

        
        dnext(v) = dnext(v)

        /*
        foreach (nm in StaticThreadNames) {  
           tNext[n,nm](v) = tNext[n,nm](v)  
        }
        */

        //foreach (l in Locks) {
        //   foreach (nm in StaticThreadNames) {
        //      held_by[l,nm]() = held_by[l,nm]()
        //   }
        //}

        foreach (l in Locks) {
           holds[l](t) = holds[l](t)
        }

    }
}


//allocating in sequential heap a node that already appears in concurrent heap.
%action assignNew_Seq(ref,trnm) {
     %t "assignNew_Seq(ref)"
     %f { runnable(tr) }
     {
        dnode(v) = dnode(v) & !r_by[ref,trnm](v)

        //Before the action, the node pointed-to by ref didn't exist in the sequential heap
        //and hence didn't have a sequential successor and also wasn't dnext
        //(when allocating a node and when updating its next field (in con or seq),
        // the node shouldn't become dnext if it's dnode).
        //If it had a successor in the concurrent heap it becomes dnext.
        dnext(v) = dnext(v) | (r_by[ref,trnm](v) & E(u) rv[n](v,u))
        
        //update to nextcon left to differencing 
     }
}

/***
* succLockField(l,trnm)
* Succesful locking of global lock l.
***/
%action succLockField(l,trnm) {
    %t "succLockField (" + l + "," + trnm + ")"
    %f  { runnable(tr) } 
    %p !E(t) (holds[l](t) & t != tr)
    %message (holds[l](tr)) -> "thread may relock a lock it already holds"
    { 
        holds[l](t_1) = (t_1 == tr)
            
    }
}

/***
* unlockField(l,trnm)
* Unlocking global lock l.
***/
%action unlockField(l,trnm) {
    %t "unlockField (" + l + "," + trnm + ")"
    %f { runnable(tr) }
    %message (!holds[l](tr)) -> "thread may unlock a lock it doesn't hold"
    {
        holds[l](t_1) = 0
        //update without message: holds[l](t_1) = holds[l](t_1) & (t_1 != tr)

    }
}

/***
* assignFromGlbField(ref,fld,trnm)
* ref = fld where ref is a local field and fld is a QUEUE field
***/
//ref is not assumed to be null before assignment.
%action assignFromGlbField(ref,fld,trnm) {
    %t ref + " = " + obj + "." + fld 
    %f { runnable(tr) }
    {   
        r_by[ref,trnm](o_1) = r_by[fld](o_1)
        
    }
}

/***
* updateNext(obj,ref,trnm)
* obj->n = ref where obj and ref are thread-local variables.
***/
//Similar to SuccCAS_Next in nbq, but without the condition on oldVal.
//obj->n is not assumed to be null before assignment.
%action updateNext(obj,ref,trnm) {
    %t "updateNext(" + obj + "," + ref + "," + trnm + ")"
    %f { runnable(tr) & r_by[obj,trnm](o_t) & rv[n](o_t,o_f),
         runnable(tr) & r_by[obj,trnm](o_t) & rv[n_seq](o_t,o_f) }
    %message (!(E(o_t) r_by[obj,trnm](o_t))) -> "possible null dereference of " + obj
    {
  
        rv[n](o_1,o_2) = ( r_by[obj,trnm](o_1) ?
                           r_by[ref,trnm](o_2) : rv[n](o_1,o_2) ) 
        
        rv[n_seq](o_1,o_2) = ( r_by[obj,trnm](o_1) ?
                               !dnode(o_1) & 
                               (rv[n_seq](o_1,o_2) | (!dnext(o_1) & rv[n](o_1,o_2)))
                               & !r_by[ref,trnm](o_2) :  
                               rv[n_seq](o_1,o_2) )

        dnext(o_1) = ( r_by[obj,trnm](o_1) ?
                       !dnode(o_1) & 
                       !A(o_2) ((rv[n_seq](o_1,o_2) | (!dnext(o_1) & rv[n](o_1,o_2))) <->
                                r_by[ref,trnm](o_2)) :
                       dnext(o_1) )
        
    }   
}

/***
* updateFieldNext_Seq(fld,ref,trnm)
* fld.next = ref, in sequential ds. 
* fld is a global field, ref is a local field.
***/

%action updateFieldNext_Seq(fld,ref,trnm) {
    %t fld + "." + n + "=" + ref + "(sequential)"
    %f { runnable(tr) & r_by[fld](o_f) & rv[n](o_f,dst) }
    //is focus formula needed? is focus formula for n_seq also needed?
    %message (!(E(o_f) r_by[fld](o_f))) -> "possible null dereference of " + fld                   
    {

        rv[n_seq](o_1,o_2) = ( r_by[fld](o_1) ?           
                               r_by[ref,trnm](o_2) & !rv[n](o_1,o_2) : 
                               rv[n_seq](o_1,o_2) )

        dnext(o_1) = ( r_by[fld](o_1) ?
                       !A(o_2) (rv[n](o_1,o_2) <-> r_by[ref,trnm](o_2)) :
                       dnext(o_1) )
        
    }   
}


/***
* assignField(fld,ref,trnm)
* fld = ref where fld is a QUEUE field
***/
//We don't assume fld==null before assignment.
%action assignField(fld,ref,trnm) {
    %t fld + "=" + ref
    %f { runnable(tr) }
    {
        r_by[fld](o_d) = r_by[ref,trnm](o_d)
   
    }   
}

/*******************
* Clear(trnm) 
* null assignment to all local variables
********************/
//We clear all local fields, no matter which action was performed (Enq or Deq).
%action Clear(trnm) {
    %t "Clear()"
    %f { runnable(tr) }
    {
        foreach (fld in LocalFields) {
            r_by[fld,trnm](o_1) = 0
        }

    }
}

/***
* assignFromNextField(ref,obj,trnm)
* ref = obj.n where ref is a local field and obj is a local field
***/
//ref is not assumed to be null before assignment.
%action assignFromNextField(ref,obj,trnm) {
    %t ref + " = " + obj + "." + fld 
    %f { runnable(tr) & r_by[obj,trnm](o_t) & rv[n](o_t,dst) }
   //focus needed becuase we read a successor
    %p E(o_h) r_by[Head_seq](o_h)
    %message (!(E(o_t) r_by[obj,trnm](o_t))) -> "possible null dereference of " + obj
    {   
        r_by[ref,trnm](o_1) = E(o_t) r_by[obj,trnm](o_t) & rv[n](o_t,o_1) 
        
    }
}

/***
* selectFieldNext_Seq(ref,fld,trnm)
* ref = fld.next, in sequential ds.
* ref is a local field and fld is a global field.
***/
//ref is NOT assumed to be null before assignment.
%action selectFieldNext_Seq(ref,fld,trnm) {
    %t ref + " = " + fld + "." + n + "(sequential)"
    %f { runnable(tr) & r_by[fld](o_f) & rv[n](o_f,dst),
         runnable(tr) & r_by[fld](o_f) & rv[n_seq](o_f,dst)
       }
    %message (!(E(o_f) r_by[fld](o_f))) -> "possible null dereference of " + fld
    {   
        r_by[ref,trnm](o_1) = E(o_f) (r_by[fld](o_f) & 
                                      ((!dnext(o_f) & rv[n](o_f,o_1)) |
                                       (dnext(o_f) & rv[n_seq](o_f,o_1)))) 
       
    }
}


/*******************
* isNull(ref,trnm)
* (ref == null)
********************/
%action isNull(ref,trnm) {
    %t ref + "== null"  
    %f { runnable(tr) }
    %p !E(o_ref) r_by[ref,trnm](o_ref)
}


/*******************
* isNotNull(ref,trnm)
* (ref != null)
********************/
%action isNotNull(ref,trnm) {
    %t ref + "!= null"  
    %f { runnable(tr) }
    %p E(o_ref) r_by[ref,trnm](o_ref)
}


/****************************
* verifySameDeqRes(trnm)
* (new_head == new_head_seq)
*****************************/
%action verifySameDeqRes(trnm) {
    %f { runnable(tr) }
    %message (!A(v) (r_by[new_head,trnm](v) <-> r_by[new_head_seq,trnm](v))) 
             -> "deq results may differ\n"
}